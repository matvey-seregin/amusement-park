
Функция ПроверитьДоступность(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки("Ok");
	Возврат Ответ;
КонецФункции

Функция СохранитьАрхив(Запрос)
	
	//Возврат СохранитьБезРазбораМультисоставногоТела(Запрос);
	
	Возврат СохранитьСРазборомМультисоставногоТела(Запрос);
	
КонецФункции

Функция СохранитьБезРазбораМультисоставногоТела(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ЗаписьЖурналаРегистрации("Получение архива");
	
	ДанныеАрхива = Запрос.ПолучитьТелоКакДвоичныеДанные();
	
	Поток = ДанныеАрхива.ОткрытьПотокДляЧтения();
	
	ВременныйКаталог = ПолучитьИмяВременногоФайла("");
		
	ЧтениеАрхива = Неопределено;
	Попытка
		ЧтениеАрхива = Новый ЧтениеФайлаАрхива(Поток);
		
		ВременныйКаталог = ПолучитьИмяВременногоФайла("");
		СоздатьКаталог(ВременныйКаталог);
	
		Для каждого ЭлементАрхива Из ЧтениеАрхива.Элементы Цикл
			
			Если НРег(ЭлементАрхива.ПолноеИмя) = "report.json" Тогда
				
				ЧтениеАрхива.Извлечь(ЭлементАрхива, ВременныйКаталог);
				ВременныйФайлJSON = ВременныйКаталог + ПолучитьРазделительПути() + ЭлементАрхива.ПолноеИмя;
				Чтение = Новый ЧтениеТекста;
				Чтение.Открыть(ВременныйФайлJSON, КодировкаТекста.UTF8);
				ОтчетОбОшибке = Чтение.Прочитать();
				Чтение.Закрыть();
				
			КонецЕсли;
			
		КонецЦикла;
		
	Исключение
		ЗаписьЖурналаРегистрации("ОшибкаСохраненияОтчета", УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Ответ;		
	КонецПопытки;
	
	//@skip-check empty-except-statement
	Попытка
		УдалитьФайлы(ВременныйКаталог);
	Исключение
		// Не обрабатываем ошибку удаления файлов
	КонецПопытки;	
	
	ДокОбъект = Документы.ОтчетОбОшибке.СоздатьДокумент();
	ДокОбъект.Дата = ТекущаяУниверсальнаяДата();
	ДокОбъект.Архив = Новый ХранилищеЗначения(ДанныеАрхива);
	ДокОбъект.ОтчетОбОшибке = ОтчетОбОшибке;
	ДокОбъект.Записать();
	
	Возврат Ответ;	
	
КонецФункции

Функция СохранитьСРазборомМультисоставногоТела(Запрос)

	Попытка

		ТипСодержимого = СтрРазделить(Запрос.Заголовки["Content-Type"], ";");
	
		Если ТипСодержимого[0] = "multipart/form-data" Тогда
	
			Разделитель = СтрРазделить(ТипСодержимого[1], "=")[1];
	
			Маркеры = Новый Массив;
			Маркеры.Добавить("--" + Разделитель);
			Маркеры.Добавить("--" + Разделитель + Символы.ПС);
			Маркеры.Добавить("--" + Разделитель + Символы.ВК);
			Маркеры.Добавить("--" + Разделитель + Символы.ВК + Символы.ПС);
			Маркеры.Добавить("--" + Разделитель + "--");
	
			Данные = Новый ЧтениеДанных(Запрос.ПолучитьТелоКакДвоичныеДанные());
	
			Данные.ПропуститьДо(Маркеры);
	
			Пока Истина Цикл
				Часть = Данные.ПрочитатьДо(Маркеры);
				Если Не Часть.МаркерНайден Тогда
					Прервать;
				КонецЕсли;
				ЧастьДанных = Новый ЧтениеДанных(Часть.ОткрытьПотокДляЧтения());
				ЗаголовкиЧасти = ПрочитатьЗаголовки(ЧастьДанных);
				ИмяЧасти = ПолучитьИмяЧасти(ЗаголовкиЧасти);
				Если ИмяЧасти = """report""" Тогда
	
	                 // Если распаковать
	                 // Архив = Новый ЧтениеZipФайла(Новый ПотокВПамяти(ЧастьДанных.Прочитать().ПолучитьБуферДвоичныхДанных()));
	                 // Архив.ИзвлечьВсе(<Путь>);
	
	                 // Если сохранить
	                 // ЧастьДанных.Прочитать().ПолучитьДвоичныеДанные().Записать(<ИмяФайла>);
	
					ДанныеАрхива = ЧастьДанных.Прочитать().ПолучитьДвоичныеДанные();
					
					ДокОбъект = Документы.ОтчетОбОшибке.СоздатьДокумент();
					ДокОбъект.Дата = ТекущаяУниверсальнаяДата();
					ДокОбъект.Архив = Новый ХранилищеЗначения(ДанныеАрхива);				
					ДокОбъект.Записать();
	
					Возврат Новый HTTPСервисОтвет(200);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	
		Возврат Новый HTTPСервисОтвет(400);
		
	Исключение
		ЗаписьЖурналаРегистрации("ОшибкаПриема", УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Новый HTTPСервисОтвет(400);
	КонецПопытки;

КонецФункции

Функция ПрочитатьЗаголовки(Чтение)

	Заголовки = Новый Соответствие;
	
	Пока Истина Цикл
		Стр = Чтение.ПрочитатьСтроку();
		Если Стр = "" Тогда
			Прервать;
		КонецЕсли;
		Части = СтрРазделить(Стр, ":");
		ИмяЗаголовка = СокрЛП(Части[0]);
		Значение = СокрЛП(Части[1]);
		Заголовки.Вставить(ИмяЗаголовка, Значение);
	КонецЦикла;
	
	Возврат Заголовки;

КонецФункции

Функция ПолучитьИмяЧасти(Заголовки)
	Описание = Заголовки.Получить("Content-Disposition");
	Свойства = СтрРазделить(Описание, ";", Ложь);
	Имя = Неопределено;
	Для Каждого Свойство Из Свойства Цикл
		Части = СтрРазделить(Свойство, "=", Ложь);
		ИмяСвойства = СокрЛП(Части[0]);
		Если ИмяСвойства <> "name" Тогда
			Продолжить;
		КонецЕсли;
		Имя = СокрЛП(Части[1]);
		Прервать;
	КонецЦикла;
	Возврат Имя;
КонецФункции

Функция КакОбработатьОшибку(Запрос)
	
	ЗаписьЖурналаРегистрации("Подтверждение отправки архива");
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("needSendReport", Истина);
	ДанныеОтвета.Вставить("userMessage", "Обработано сервисом сбора ошибок");
	
	ТелоОтвета = ЗаписатьЗначениеJSON(ДанныеОтвета);
	
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки(ТелоОтвета);
	Ответ.Заголовки.Вставить("Content-Type", "application/json");
		
	Возврат Ответ;
	
КонецФункции
