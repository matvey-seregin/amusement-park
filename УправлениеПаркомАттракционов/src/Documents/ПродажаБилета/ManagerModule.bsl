
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Переопределяет настройки печати для объекта.
//
// Параметры:
//  Настройки - см. УправлениеПечатью.НастройкиПечатиОбъекта.
//
Процедура ПриОпределенииНастроекПечати(Настройки) Экспорт
       Настройки.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
    
    Команда = КомандыПечати.Добавить();	
	Команда.Идентификатор = "Билет";
	Команда.Представление = НСтр("ru = 'Билет'");
	Команда.ПроверкаПроведенияПередПечатью = Истина;
	Команда.Порядок = 10;
    
    Команда = КомандыПечати.Добавить();	
	Команда.Идентификатор = "Реклама";
	Команда.Представление = НСтр("ru = 'Реклама'");
	Команда.ПроверкаПроведенияПередПечатью = Истина;
	Команда.Порядок = 20;
    
    Команда = КомандыПечати.Добавить();	
	Команда.Идентификатор = "Билет,Реклама";
	Команда.Представление = НСтр("ru = 'Комплект с рекламой при продаже'");
	Команда.ПроверкаПроведенияПередПечатью = Истина;
	Команда.Порядок = 30;
	
	 
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//	МассивОбъектов – Массив – ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати – Структура – дополнительные настройки печати;
//  КоллекцияПечатныхФорм – ТаблицаЗначений – сформированные табличные документы (выходной параметр)
//  ОбъектыПечати – СписокЗначений – значение – ссылка на объект;
//                                            представление – имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода – Структура – дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Билет");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьБилета(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Билет'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.ПродажаБилета.ПФ_MXL_Билет";
	КонецЕсли;	
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Реклама");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьРекламы(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Реклама'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.ПродажаБилета.ПФ_MXL_Реклама";
	КонецЕсли;
	
КонецПроцедуры

// Обработчик перехода на новую версию
// Переносит значения из реквизитов в новую табличную часть
// 
Процедура ПеренестиНоменклатуруВТабличнуюЧасть() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаБилета.Ссылка
		|ИЗ
		|	Документ.ПродажаБилета КАК ПродажаБилета
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПродажаБилета.ПозицииПродажи КАК ПродажаБилетаПозицииПродажи
		|		ПО ПродажаБилетаПозицииПродажи.Ссылка = ПродажаБилета.Ссылка
		|		И ПродажаБилетаПозицииПродажи.НомерСтроки = 1
		|ГДЕ
		|	ПродажаБилета.УдалитьНоменклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	И ПродажаБилетаПозицииПродажи.Ссылка ЕСТЬ NULL";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Строка = ДокОбъект.ПозицииПродажи.Добавить();
		Строка.Номенклатура = ДокОбъект.УдалитьНоменклатура;
		Строка.Цена = ДокОбъект.УдалитьЦена;
		Строка.Количество = 1;
		Строка.Сумма = Строка.Цена;
		
		ДокОбъект.ОбменДанными.Загрузка = Истина;
		ДокОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
 	Ограничение.Текст =
 	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Филиал)";
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьБилета(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПродажаБилета.ПФ_MXL_Билет");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПродажаБилета.Номер,
		|	ПродажаБилета.Дата,
		|	ПродажаБилета.СуммаДокумента,
		|	ПродажаБилета.Ссылка,
		|	ПродажаБилета.ПозицииПродажи.(
		|		Номенклатура,
		|		Цена,
		|		Количество,
		|		Сумма,
		|		Номенклатура.КоличествоПосещений КАК КоличествоПосещений)
		|ИЗ
		|	Документ.ПродажаБилета КАК ПродажаБилета
		|ГДЕ
		|	ПродажаБилета.Ссылка В (&Ссылка)";
		
	Запрос.Параметры.Вставить("Ссылка", МассивОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОписаниеТовара = Макет.ПолучитьОбласть("ОписаниеТовара");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	
	ТабличныйДокумент.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ПараметрыОбласти = Новый Структура;
		ПараметрыОбласти.Вставить("Дата", Формат(Выборка.Дата, "ДЛФ=D;"));
		ПараметрыОбласти.Вставить("Номер", УдалитьЛидирующиеНули(Выборка.Номер));
		
		ОбластьЗаголовок.Параметры.Заполнить(ПараметрыОбласти);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ВыборкаПозицииПродажи = Выборка.ПозицииПродажи.Выбрать();
		
		Пока ВыборкаПозицииПродажи.Следующий() Цикл
		
			ОписаниеТовара.Параметры.Заполнить(ВыборкаПозицииПродажи);
			ТабличныйДокумент.Вывести(ОписаниеТовара);
		
		КонецЦикла;
		
		ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(Выборка.Номер, 3, 120);
		
		КартинкаQRКода = Новый Картинка(ДанныеQRКода);
		Подвал.Рисунки.QRКод.Картинка = КартинкаQRКода;
				
		Подвал.Параметры.Заполнить(Выборка);
		ТабличныйДокумент.Вывести(Подвал);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
		ВставлятьРазделительСтраниц = Истина;
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьРекламы(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПродажаБилета.ПФ_MXL_Реклама");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПродажаБилета.Номер,
		|	ПродажаБилета.Дата,
		|	ПродажаБилета.Ссылка
		|ИЗ
		|	Документ.ПродажаБилета КАК ПродажаБилета
		|ГДЕ
		|	ПродажаБилета.Ссылка В (&Ссылка)";
		
	Запрос.Параметры.Вставить("Ссылка", МассивОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	
	ТабличныйДокумент.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ПараметрыОбласти = Новый Структура;
		ПараметрыОбласти.Вставить("Дата", Формат(Выборка.Дата, "ДЛФ=D;"));
		ПараметрыОбласти.Вставить("Номер", УдалитьЛидирующиеНули(Выборка.Номер));
		
		ОбластьЗаголовок.Параметры.Заполнить(ПараметрыОбласти);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
		ВставлятьРазделительСтраниц = Истина;
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция УдалитьЛидирующиеНули(Номер)
	Результат = Номер;
	Пока СтрНачинаетсяС(Результат, "0") Цикл
		Результат = Сред(Результат, 2);
	КонецЦикла;
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецЕсли

