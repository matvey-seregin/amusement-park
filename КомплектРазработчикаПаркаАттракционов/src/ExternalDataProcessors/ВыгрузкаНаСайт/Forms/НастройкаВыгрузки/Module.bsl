
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ДополнительнаяОбработкаСсылка) Тогда
		Настройки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Параметры.ДополнительнаяОбработкаСсылка, "ХранилищеНастроек").Получить();
		Если ТипЗнч(Настройки) = Тип("Структура") Тогда
			КаталогХраненияФайлов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Настройки, "КаталогХраненияФайлов")
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если Не ЗначениеЗаполнено(Параметры.ДополнительнаяОбработкаСсылка) Тогда
		ПредупреждениеАсинх(НСтр("ru = 'Для сохранения настроек форму настроек необходимо открыть из справочника Дополнительные отчеты и обработки.'"));
		Возврат;
	КонецЕсли;
	
	СохранитьНаСервере();
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНаСервере()
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементыБлокировки = Блокировка.Добавить("Справочник.ДополнительныеОтчетыИОбработки");
		ЭлементыБлокировки.УстановитьЗначение("Ссылка", Параметры.ДополнительнаяОбработкаСсылка);
		Блокировка.Заблокировать();
		
		СпрОбъект = Параметры.ДополнительнаяОбработкаСсылка.ПолучитьОбъект();
		
		Настройки = Новый Структура;
		Настройки.Вставить("КаталогХраненияФайлов", КаталогХраненияФайлов);
		
		СпрОбъект.ХранилищеНастроек = Новый ХранилищеЗначения(Настройки);
		
		СпрОбъект.Записать();	
			
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти






